package domain.systems;

import core.Frame;
import data.Data;
import data.core.ColorKey;
import data.resources.TileRegistry;
import ecs.System;
import h2d.Bitmap;
import h2d.Object;
import h2d.Text;
import haxe.EnumTools;

typedef DebugInfo =
{
	ob:Object,
	fps:Text,
	pos:Text,
	clock:Text,
	entities:Text,
	drawCalls:Text,
	cursor:Bitmap,
}

class DebugInfoSystem extends System
{
	public var debugInfo:DebugInfo;

	private var cursor_x:Int;
	private var cursor_y:Int;
	private var cursor_z:Int;

	public function new()
	{
		renderDebugInfo();
	}

	override function update(frame:Frame)
	{
		var w = game.input.mouse.toWorld().toIntPoint();
		var px = game.input.mouse.toPx().floor().toString();
		var wtext = w.toString();
		var fps = frame.fps.floor();

		var sx = game.input.mouse.x.floor();
		var sy = game.input.mouse.y.floor();

		var ray = world.terrain.raycast.Get(sx, sy);
		var terrainText = EnumValueTools.getName(ray.terrain);

		debugInfo.fps.text = game.app.engine.fps.floor().toString() + ' ' + frame.fps.floor().toString();
		debugInfo.fps.color = getFpsColor(fps).toHxdColor();
		debugInfo.pos.text = '$wtext $px Z(${game.camera.zoom}) $sx,$sy ${ray.success}, ${ray.x},${ray.y},${ray.z} [$terrainText]';
		debugInfo.entities.text = 'entities ${game.registry.size.toString()}';
		debugInfo.clock.text = '${world.clock.tick.floor()} (${world.clock.speed})';
		debugInfo.drawCalls.text = 'draw ${game.app.engine.drawCalls}';

		if (ray.success)
		{
			var x = ray.x.floor();
			var y = ray.y.floor();
			var z = ray.z.floor() + 1;

			debugInfo.cursor.color = 0xff00ff.toHxdColor();
			debugInfo.cursor.visible = true;

			if (x != cursor_x || y != cursor_y || z != cursor_z)
			{
				trace('cursor moved');
				cursor_x = x;
				cursor_y = y;
				cursor_z = z;

				debugInfo.cursor.remove();

				debugInfo.cursor.tile.setCenterRatio(0, (z / 4));

				var p = world.terrain.ob.worldToTilePx(x, y, 0);

				debugInfo.cursor.x = p.x;
				debugInfo.cursor.y = p.y;

				world.terrain.ob.add(debugInfo.cursor, 0);
				world.terrain.ob.ysort(0);
			}
		}
		else
		{
			debugInfo.cursor.visible = false;
		}

		if (game.input.lmb)
		{
			if (world.terrain.get(cursor_x, cursor_y, cursor_z - 1) == EMPTY)
			{
				return;
			}

			if (world.terrain.get(cursor_x, cursor_y, cursor_z) == EMPTY)
			{
				world.terrain.set(cursor_x, cursor_y, cursor_z, DIRT);
				world.terrain.ob.ysort(0);
				trace('lmb!');
			}
		}
	}

	function getFpsColor(fps:Int):Int
	{
		if (fps < 60)
		{
			return 0xbe7474;
		}

		if (fps < 100)
		{
			return 0xe0de62;
		}

		return 0x92e08b;
	}

	override function teardown()
	{
		debugInfo.ob.remove();
		debugInfo.cursor.remove();
	}

	private function renderDebugInfo()
	{
		var ob = new Object();
		ob.x = 16;
		ob.y = 16;

		var fps = Data.Fonts.text(FNT_BIZCAT, ob);
		fps.y = 0;

		var pos = Data.Fonts.text(FNT_BIZCAT, ob);
		pos.color = ColorKey.C_WHITE.toHxdColor();
		pos.y = 16;

		var entities = Data.Fonts.text(FNT_BIZCAT, ob);
		entities.color = ColorKey.C_WHITE.toHxdColor();
		entities.y = 32;

		var clock = Data.Fonts.text(FNT_BIZCAT, ob);
		clock.color = ColorKey.C_WHITE.toHxdColor();
		clock.y = 48;

		var drawCalls = Data.Fonts.text(FNT_BIZCAT, ob);
		drawCalls.color = ColorKey.C_WHITE.toHxdColor();
		drawCalls.y = 64;

		var cursor = new Bitmap(Data.Tiles.get(TK_CURSOR));

		debugInfo = {
			ob: ob,
			fps: fps,
			pos: pos,
			entities: entities,
			clock: clock,
			drawCalls: drawCalls,
			cursor: cursor,
		};

		game.render(HUD, ob);
		// game.render(OVERLAY, cursor);
	}
}
